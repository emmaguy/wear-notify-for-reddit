apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 'android-N'
    buildToolsVersion '24.0.0 rc4'

    signingConfigs {
        release
    }
    defaultConfig {
        applicationId "com.emmaguy.todayilearned"
        minSdkVersion 'N'
        targetSdkVersion 'N'
        versionCode 51
        versionName '3.1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'UnusedAttribute', 'Overdraw', 'UnusedResources', 'ClickableViewAccessibility'
    }
}

dependencies {
    compile project(':sharedlib')

    // Comms
    compile 'com.google.android.gms:play-services-wearable:8.4.0'

    // Ui
    compile 'com.google.android.support:wearable:2.0.0-alpha1'
}

def Properties props = new Properties()
if (project.hasProperty("signing.properties") && file(project.property("signing.properties")).canRead()) {
    props.load(new FileInputStream(file(project.property("signing.properties"))))

    if (props != null && props.containsKey('keystore') && props.containsKey('keystore.password') && props.containsKey('keyalias') && props.containsKey('keyalias.password')) {
        android.signingConfigs.release.storeFile = file(props['keystore'])
        android.signingConfigs.release.storePassword = props['keystore.password']
        android.signingConfigs.release.keyAlias = props['keyalias']
        android.signingConfigs.release.keyPassword = props['keyalias.password']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
